/*
A bitset for Oak
Revised for Oak 0.3.0, which added arrays.

Jordan Dehmel, 2023
jdehmel@outlook.com
*/

package!("std");
include!("std/opt.oak");
include!("extra/bit_print.oak");
use_rule!("std");

/*
A bitset for Oak. Size is a positive integer representing the
number of BYTES, not bits.
*/
let bitset<size>: struct
{
    data: [size]u8,
}
pre
{
    opt<bool>;
}
post
{
    Get<size>(_: ^bitset<size>, _: u128);

    set<size>(_: ^bitset<size>, _: u128);
    clear<size>(_: ^bitset<size>, _: u128);
    flip<size>(_: ^bitset<size>, _: u128);

    print<size>(_: ^bitset<size>);
}

////////////////////////////////////////////////////////////////
// Implementations
////////////////////////////////////////////////////////////////

let Get<size>(self: ^bitset<size>, i: u128) -> opt<bool>
{
    let out: opt<bool>;
    out.wrap_none();

    if i > to_u128(size * 8)
    {
        return out;
    }

    let byte_i: u128 = i / to_u128(8);
    let bit_i: u8 = to_u8(i % to_u128(8));
    let got: u8 = Get(self.data, byte_i);
    let mask: u8 = 1 << bit_i;
    got &= mask;

    if got != 0
    {
        out.wrap_some(true);
    }
    else
    {
        out.wrap_some(false);
    }

    out
}

let set<size>(self: ^bitset<size>, i: u128) -> void
{
    let byte_i: u128 = i / to_u128(8);
    let bit_i: u8 = to_u8(i % to_u128(8));
    let mask: u8 = 1 << bit_i;

    ^Get(self.data, byte_i) |= mask;
}

let clear<size>(self: ^bitset<size>, i: u128) -> void
{
    let byte_i: u128 = i / to_u128(8);
    let bit_i: u8 = to_u8(i % to_u128(8));
    let mask: u8 = 1 << bit_i;
    mask = 255 - mask;

    ^Get(self.data, byte_i) &= mask;
}

let flip<size>(self: ^bitset<size>, i: u128) -> void
{
    let res = self.Get(i);

    if unwrap(res)
    {
        self.clear(i);  
    }
    else
    {
        self.set(i);
    }
}

let print<size>(what: ^bitset<size>) -> void
{
    bit_print<bitset<size>>(_: ^bitset<size>);
    bit_print(what);
}
